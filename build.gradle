plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withSourcesJar()
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api group: 'com.jhlabs', name: 'filters', version: filtersVersion
    api group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    api group: 'org.javassist', name: 'javassist', version: javaAssistVersion

    testImplementation platform(group: 'org.junit', name: 'junit-bom', version: junitVersion)
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
    testRuntimeOnly group: 'org.junit.jupiter', name:'junit-jupiter-engine'

    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Roxtools',
                'Implementation-Version': archiveVersion,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date().format('yyyy-MM-dd HH:mm:ss')
        )
    }
}

publishing {
    repositories {
        maven {
            url = uri("${nexusUrl}/repository/${nexusRepository}/")
            credentials {
                username "${nexusUsername}"
                password "${nexusPassword}"
            }
        }
        publications {
            theLibrary(MavenPublication) {
                from components.java
            }
        }
    }
}
